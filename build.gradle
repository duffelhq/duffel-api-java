plugins {
    id 'idea'
    id 'java'
    id 'jvm-test-suite'
    id 'maven-publish'
    id 'signing'
    id 'com.palantir.git-version' version "3.0.0"
    id "io.freefair.lombok" version "8.0.1"
}

repositories {
    mavenCentral()
}

ext {
    jacksonVersion = '2.15.2'
    jacksonDatabindVersion = '2.15.2'
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    testImplementation 'commons-io:commons-io:2.13.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mock-server:mockserver-junit-jupiter-no-dependencies:5.15.0'
    testImplementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.20.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    filter {
        // Don't execute the examples by default, as these create test orders and need a token to run
        excludeTestsMatching("com.duffel.example.*")
    }
}

testing {
    suites {
        test {}

        example(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            dependencies {
                implementation project
            }

            sources {
                java {
                    srcDirs = ["src/test/java/com/duffel/example"]
                }
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

configurations {
    exampleImplementation.extendsFrom(testImplementation)
    exampleRuntimeOnly.extendsFrom(testRuntimeOnly)
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        release(MavenPublication) {
            from components.java
            if (project.hasProperty('sign')) {
                signing {
                    useInMemoryPgpKeys(
                        property('SIGNING_KEY_ID'),
                        property('SIGNING_KEY'),
                        property('SIGNING_PASSWORD')
                    )
                    sign publishing.publications.release
                    sign configurations.archives
                }
            }
            pom {
                name = 'duffel-api'
                group = 'com.duffel'
                version = gitVersion(null)
                description = 'Java SDK for Duffel'
                url = 'http://www.duffel.com'

                scm {
                    url = 'https://github.com/duffelhq/duffel-api-java'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/duffelhq/duffel-api-java/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'Duffel'
                        name = 'Duffel'
                        email = 'client-libraries@duffel.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty("SONATYPE_USERNAME")
                password = findProperty("SONATYPE_PASSWORD")
            }
        }
    }
}

javadoc {
    options.addBooleanOption('html5', true)
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
group = "com.duffel"
archivesBaseName = "api"
version = "0.0.1"
